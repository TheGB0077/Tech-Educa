## Controle de Fluxo

O controle de fluxo de um programa,
consiste na capacidade de fazer o seu programa tomar decis√µes.
Pense da seguinte forma:
`Uma escolha √© como uma bifurca√ß√£o na estrada, voc√™ precisa escolher um caminho para seguir.`

![escolha1](img/escolha_estrada.svg)

<p style="font-size:10px"><a href="https://www.freepik.com/free-vector/way-concept-illustration_7273255.htm" target="_blank">Imagem por storyset</a> em Freepik</p>

### Comando `Se`

Na programa√ß√£o, tamb√©m precisamos fazer escolhas, e para isso, utilizamos o comando `se` (if em ingl√™s).
Veja um exemplo onde precisamos verificar se uma pessoa √© maior de idade:

::: code-group

```portugol [Portugol Studio]
programa {
    funcao inicio() {
        inteiro idade

        escreva("Digite sua idade: ")
        leia(idade)

        se (idade >= 18) {
            escreva("Voc√™ √© maior de idade!\n")
        }
    }
}
```

```portugol [VisuAlg]
algoritmo "Maior de Idade"
Var
    idade: inteiro
inicio
    escreva("Digite sua idade: ")
    leia(idade)

    se (idade >= 18) entao
        escreval("Voc√™ √© maior de idade!")
    fimse
fimalgoritmo
```

:::

Essa verifica√ß√£o √© feita atrav√©s de uma express√£o l√≥gica, que √© computada para um valor do tipo logico (verdadeiro ou falso). No caso acima, a express√£o `idade >= 18` √© computada como verdadeiro se a idade informada for `maior ou igual a 18`, e falso caso contr√°rio.

Outros exemplos de express√µes l√≥gicas incluem:

- `idade > 18` ‚áî idade maior que 18
- `idade < 18` ‚áî idade menor que 18
- `idade == 18` ‚áî idade igual a 18

> üëÜ `idade = 18` no VisuAlg

- `idade != 18` ‚áî idade diferente de 18

> üëÜ `idade <> 18` no VisuAlg

- `idade <= 18` ‚áî idade menor ou igual a 18

O exemplo acima tem uma resposta para o caso em que a idade √© maior ou igual a 18, mas e se a idade for menor que 18? Para isso, utilizamos o comando `senao` (else em ingl√™s), que √© executado caso a express√£o l√≥gica seja falsa.

::: code-group

```portugol [Portugol Studio]
programa {
    funcao inicio() {
        inteiro idade

        escreva("Digite sua idade: ")
        leia(idade)

        se (idade >= 18) {
            escreva("Voc√™ √© maior de idade!\n")
        } senao {
            escreva("Voc√™ √© menor de idade!\n")
        }
    }
}
```

```portugol [VisuAlg]
algoritmo "Maior de Idade"
Var
    idade: inteiro
inicio
    escreva("Digite sua idade: ")
    leia(idade)

    se (idade >= 18) entao
        escreval("Voc√™ √© maior de idade!")
    senao
        escreval("Voc√™ √© menor de idade!")
    fimse
fimalgoritmo
```

:::

### Operadores L√≥gicos

Al√©m dos operadores relacionais vistos acima, tamb√©m temos os operadores l√≥gicos, que s√£o utilizados para combinar express√µes l√≥gicas. Veja a tabela abaixo:

| Operador |    Significado    |           Exemplo           |
|:--------:|:-----------------:|:---------------------------:|
|   `e`    |     L√≥gica E      | `idade >= 18 e idade <= 60` |
|   `ou`   |     L√≥gica OU     | `idade < 18 ou idade > 60`  |
|  `nao`   | Nega√ß√£o da L√≥gica |     `nao (idade >= 18)`     |

A l√≥gica E (`e`) s√≥ √© verdadeira se as duas express√µes forem verdadeiras.
J√° a l√≥gica OU (`ou`) √© verdadeira se, pelo menos, **uma das express√µes for verdadeira**.
Por fim, a nega√ß√£o da l√≥gica (`nao`) inverte o valor da express√£o l√≥gica.

Se voc√™ √© familiar com o conceito de tabelas verdade, ent√£o tudo isso √© bem intuitivo. Se n√£o, ent√£o vale a pena conferir o v√≠deo abaixo para aprender mais sobre o assunto:

<Youtube id="uDNNBiST3ls"/>

### Exemplo de Decis√µes Aninhadas

Nos casos mais complexos, pode haver a necessidade de avaliar m√∫ltiplas condi√ß√µes em um programa. Veja o exemplo abaixo:

::: code-group

```portugol [Portugol Studio]
programa {
    funcao inicio() {        
        cadeia alerta

        escreva("Qual o alerta: ")
        leia(alerta)

        se (alerta == "Nenhum") {
            escreva("Voc√™ est√° seguro!\n")
        } senao {
            se (alerta == "Furac√£o") {
                escreva("Feche as janelas!\n")
            } senao se (alerta == "Terremoto") {
                escreva("Se abrigue embaixo da mesa!\n")
            } senao {
                escreva("Procure um lugar seguro!\n")
            }
        }
    }
}
```

```portugol [VisuAlg]
algoritmo "Alerta"
Var
    alerta: caracter
inicio
    escreva("Qual o alerta: ")
    leia(alerta)

    se (alerta = "Nenhum") entao
        escreval("Voc√™ est√° seguro!")
    senao
        se (alerta = "Furac√£o") entao
            escreval("Feche as janelas!")
        senao
            se (alerta = "Terremoto") entao
                escreval("Se abrigue embaixo da mesa!")
            senao
                escreval("Procure um lugar seguro!")
            fimse
        fimse
    fimse
fimalgoritmo
```

:::

Observamos no exemplo acima que o comando `se` pode ser aninhado dentro de outro comando `se`, formando uma estrutura de decis√£o aninhada. Os principais pontos a serem observados s√£o:

1. Avaliamos primeiro se n√£o havia nenhum alerta (`alerta == "Nenhum"`), caso contr√°rio, prosseguimos para descobrir qual era o alerta.
2. Com a combina√ß√£o de `senao` e `se`, podemos avaliar v√°rios casos em sequ√™ncia para descobrir qual a melhor decis√£o a ser tomada.

<style lang="css">

img[alt=escolha1] {
    width: 60%;
    display: block;
}

</style>
